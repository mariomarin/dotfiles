#!/usr/bin/env nu
# Install Homebrew packages from Brewfile
# hash: {{ include "private_dot_config/brew/Brewfile" | sha256sum }}

let brewfile = $"($env.HOME)/.config/brew/Brewfile"

if not ($brewfile | path exists) {
    exit 0
}

# Install Homebrew if not present
if (which brew | is-empty) {
    print "Installing Homebrew..."
    bash -c '/bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"'
}

print "Installing Homebrew packages..."
brew bundle --file $brewfile

# Install Karabiner-DriverKit (required for kanata)
let driverkit_app = "/Applications/.Karabiner-VirtualHIDDevice-Manager.app"
if not ($driverkit_app | path exists) {
    print "Installing Karabiner-DriverKit..."
    let release = http get https://api.github.com/repos/pqrs-org/Karabiner-DriverKit-VirtualHIDDevice/releases/latest
    let pkg_url = $release.assets | where name =~ '\.pkg$' | first | get browser_download_url
    let tmp_pkg = $"/tmp/karabiner-driverkit.pkg"
    http get $pkg_url | save -f $tmp_pkg
    sudo installer -pkg $tmp_pkg -target /
    rm $tmp_pkg
    print "✓ Karabiner-DriverKit installed"
    print "  Run: /Applications/.Karabiner-VirtualHIDDevice-Manager.app/Contents/MacOS/Karabiner-VirtualHIDDevice-Manager activate"
}

# Install pay-respects via cargo (not in Homebrew)
if (which pay-respects | is-empty) {
    print "Installing pay-respects via cargo..."
    cargo install pay-respects
    print "✓ pay-respects installed"
}
